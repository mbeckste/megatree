// Nextflow workflow for
// (1) ClustalOmega MSA generation
// (2) Reconstruction of evolutionary tree with iqtree
// (3) Tree visualization using R/ggtree


// Process for multiple sequence alignment generation using clustalomega.
process clustalomega{
   label 'highmem_large'
   container "biocontainers/clustal-omega:v1.2.1_cv5"
//   publishDir params.outdir, mode: 'copy'
   shell '/bin/bash', '-euo', 'pipefail'
   debug false

   input: 
     path(InputFastaFile)

   output:
     path "${InputFastaFile}.msa"
 
  script: 
  """
    clustalo -i $InputFastaFile -o ${InputFastaFile}.msa --outfmt=fa
  """
}


// Phylogenetic analysis and reconstruction of evolutionary tree
process iqtree {
  label 'highmem_medium'
  container "staphb/iqtree:latest"
//  publishDir params.outdir, mode: 'copy'
  shell '/bin/bash', '-euo', 'pipefail'
  debug false
	 
  input:
    path(InputMSA)

  output:
    path "${InputMSA}.*"

  script:
  """
   iqtree -nt ${params.threads} -s $InputMSA  -m LG 
  """
}

// Visualize tree in Newick format with R ggtree package
process visualizetree {
  label 'small'
//  container "quay.io/biocontainers/bioconductor-ggtree:3.8.0--r43hdfd78af_0"
  container "xushuangbin/ggtreeextraarticleenv:latest"
  publishDir params.outdir, mode: 'copy'

  input:
   path(inputFile)
   path(iqtreeFiles)
   
  output:
   path("${inputFile}.tree*.pdf")

  script:
  """
   #!/usr/local/bin/Rscript
   library("ggplot2")
   library("ggtree")

   # Read Newick formatted file generated by iqtree
   tree <- read.tree(paste0("$inputFile",".treefile" ))

   # Plot 1: Normal tree 
   p1 = ggtree(tree) +
        geom_tiplab(size=2)

   # Plot 2: Tree with circular layout
   p2 = ggtree(tree,layout="circular") +
        geom_tiplab(aes(angle=angle),color="darkgreen",size=2)

   # Plot 3: Tree and  multiple alignment
   msa_view_start = ${params.msa_view_start}
   msa_view_end = ${params.msa_view_end}
   mytitle = "${params.title}"
   mysubtitle = "${params.subtitle}"

   p3 = ggtree(tree,branch.length='none') +
        geom_tiplab(color="blue",size=1.7,offset=.5) +
        geom_nodepoint(color="#b5e521", alpha=1.0, size=2) + 
        geom_tippoint(color="#FDAC4F", alpha=.5,shape=15, size=2)
   p3 = msaplot(p3,"$inputFile",offset=18,width=2,height=0.8,window=c(msa_view_start,msa_view_end))
   p3 = p3 + guides(fill=guide_legend(title="Amino acids")) +
        theme(plot.caption = element_text(color="red",size=7))
     
   
   # Title, subtitle, captions ...    
   mytitle ="Evolution of Influenza A virus subtyp H3N2"
   mysubtitle ="Based on hemagglutinin 3 (H3) AA sequences."
   mycap1=paste0("File: ","$inputFile")
   mycap2=paste0("MSA is shown for AA positions ",msa_view_start,"-",msa_view_end,". File: ","$inputFile")
   p1 = p1 + labs(title=mytitle,subtitle=mysubtitle)
   p2 = p2 + labs(title=mytitle,subtitle=mysubtitle,caption=mycap1)
   p3 = p3 + labs(title=mytitle,subtitle=mysubtitle,caption=mycap2)

   # Save plots as PDF 
   ggsave(filename=paste0("$inputFile",".tree1",".pdf"),p1,device="pdf")
   ggsave(filename=paste0("$inputFile",".tree2",".pdf"),p2,device="pdf")
   ggsave(filename=paste0("$inputFile",".tree3",".pdf"),p3,device="pdf")

  """
}




workflow {
   clustalomega(params.inputfile) | iqtree
   visualizetree(clustalomega.out,iqtree.out)
}
